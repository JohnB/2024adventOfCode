<!-- vim: syntax=markdown -->

# Day 04

```elixir
Mix.install([
  {:kino, "~> 0.14.2"}
])

IEx.Helpers.c("/Users/johnb/dev/2024adventOfCode/advent_of_code.ex")
alias AdventOfCode, as: AOC
alias Kino.Input

# Note: when making the next template, something like this works well:
#   `cat day04.livemd | sed 's/04/04/' > day04.livemd`
#
# When inspecting lists of numbers, use "charlists: :as_lists"
```

## Installation and Data

```elixir
input_p1example = Kino.Input.textarea("Example Data")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_p1puzzleInput = Kino.Input.textarea("Puzzle Input")
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
input_source_select =
  Kino.Input.select("Source", [{:example, "example"}, {:puzzle_input, "puzzle input"}])
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
p1data = fn ->
  (Kino.Input.read(input_source_select) == :example &&
     Kino.Input.read(input_p1example)) ||
    Kino.Input.read(input_p1puzzleInput)
end
```

## Solution

```elixir
defmodule Day04 do
  def solve1(text) do
    grid = AOC.as_grid(text)

    grid
    |> AOC.grid_cells()
    |> Enum.filter(fn x_id -> grid[x_id] == "X" end)
    |> Enum.reduce(0, fn x_id, count ->
      grid
      |> AOC.neighbors8(x_id)
      |> Enum.reduce(count, fn m_id, acc ->
        delta = m_id - x_id
        a_id = m_id + delta
        s_id = m_id + 2 * delta
        
        if [grid[m_id], grid[a_id], grid[s_id]] == ["M", "A", "S"]
          && !spans_edge?(grid, m_id, a_id, s_id) do
          acc + 1
        else
          acc
        end
      end)
    end)
  end

  def solve2(text) do
    grid = AOC.as_grid(text)

    grid
    |> AOC.grid_cells()
    |> Enum.filter(fn a_id -> grid[a_id] == "A" end)
    # Disallow "A" on any edge (thus less than 8 neighbors)
    |> Enum.filter(fn a_id -> Enum.count(AOC.neighbors8(grid, a_id)) == 8 end)
    |> Enum.filter(fn a_id ->
      corners = [
        grid[a_id - 1 - grid.grid_width], # up-left
        grid[a_id + 1 - grid.grid_width], # up-right
        grid[a_id - 1 + grid.grid_width], # down-left
        grid[a_id + 1 + grid.grid_width]  # down-right
      ]
      mmss = Enum.sort(corners) |> Enum.join()
      
      mmss == "MMSS" && 
        List.first(corners) != List.last(corners) # disallow MAM and SAS
    end)
    |> Enum.count()
  end

  def spans_edge?(grid, m, a, s) do
    mx = AOC.grid_x(grid, m)
    ax = AOC.grid_x(grid, a)
    sx = AOC.grid_x(grid, s)
    
    # MAS letters found on both edges?
    0 in [mx, ax, sx] && (grid.grid_width - 1) in [mx, ax, sx]
  end
end

# Example:
# MMMSXXMASM
# MSAMXMSMSA
# AMXSXMAAMM
# MSAMASMSMX
# XMASAMXAMM
# XXAMMXXAMA
# SMSMSASXSS
# SAXAMASAAA
# MAMMMXMMMM
# MXMXAXMASX

p1data.()
|> Day04.solve1()
|> IO.inspect(label: "\n*** Part 1 solution (example: 18)")
# 2718

p1data.()
|> Day04.solve2()
|> IO.inspect(label: "\n*** Part 2 solution (example: 9)")
# 2046

```
